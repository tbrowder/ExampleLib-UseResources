#!/usr/bin/env raku

use Module::Utils;

my $s;

my $nlines;
if not @*ARGS {
    print qq:to/HERE/;
    Usage: {$*PROGRAM.basename} <number of lines [min 2] > | all

    Shows the contents of the distribution's "./resources' files.
    HERE
    exit;
}

my $debug = 0;
my $arg = @*ARGS.shift;
$nlines = $arg ~~ /^\d+$/ ?? $arg !! 0;
++$nlines if $nlines == 1;

my @rpaths = get-resources-paths :$debug;
say "Resource paths:";
say "  $_" for @rpaths;

say "Contents:";
for @rpaths -> $f {
    my $s = get-content $f, :$nlines;
    unless $s {
        say "==File '$f' is not accessible.";
        next;
    }

    say "==File '$f':";
    for $s.lines.kv -> $i is copy, $v {
        my $n = sprintf "%2d", ++$i;
        say "    line $n: $v";
    }
    say();
}
